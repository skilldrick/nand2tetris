// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register (in=resetMuxOut, load=true, out=registerOut, out=out);
    Inc16    (in=registerOut, out=incOut);

    // Mux16s are opposite order to if order, because last one to
    // be true wins
    Mux16    (a=registerOut, b=incOut, sel=inc, out=incMuxOut);
    Mux16    (a=incMuxOut, b=in, sel=load, out=loadMuxOut);
    Mux16    (a=loadMuxOut, b=false, sel=reset, out=resetMuxOut);
}
